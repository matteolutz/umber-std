import "core.ub";
import "heap.ub";

// Implement logic, to include system relevant things depending on actual system
import "linux.ub";

import "cstr.ub";

fun print_char_ptr(arr: char *, len: u64): u64 {
    return sys_write(1, arr, len);
};

fun print_str(str: string, len: u64): u64 {
    return print_char_ptr(str as char *, len);
};

fun print_strl(str: string): u64 {
    return print_str(str, cstrlen(str as char *));
};

fun print_char_ptr_l(ptr: char *): u64 {
    return print_char_ptr(ptr, cstrlen(ptr));
};

fun throw(msg: char *, len: u64): void {
    print_strl("\n\n\u001b[31m[ERROR]\u001b[0m ");
    print_char_ptr(msg, len);
    print_strl("\n\nExiting with status code \u001b[31;1m1\u001b[0m...\n\n");
    sys_exit(1);
    return;
};

fun throwc(msg: char *): void {
    return throw(msg, cstrlen(msg));
};

fun atoi(str: char *): u64 {
    let mut res: u64 = 0;

    for let mut i: u64 = 0; *str[i] != '\0'; i += 1 {
        res = res * 10 + (*str[i] - '0') as u64;
    };

    return res;
};

fun itoa(n: u64, base: u64, buf_end: char *mut): u64 {
    (buf_end as char *mut) @= '\0';
    let mut current: char *mut = buf_end-1;

    let mut t: u64 = n;

    while true {
        let mut new: u64 = ('0' as u64) + (t % base);

        // Skips from ascii numbers to letters, to correctly represent hex numbers
        if new > 57 {
            new = (new % 57) + ('A' as u64) - 1;
        };

        (current -= sizeof[char]) @= new as char;

        t /= base;

        if t == 0 { break; };
    };

    if n < 0 {
        (current -= sizeof[char]) @= '-';
    };

    return (buf_end - current) as u64;
};

fun print_num(n: u64, base: u64): void {
    let bufferSize: u64 = 128;

    let begin_ptr: char *mut = malloc(bufferSize) as char *mut;
    let buf_end: char *mut = begin_ptr + bufferSize;

    let size: u64 = itoa(n, base, buf_end);

    print_char_ptr((buf_end - size) as char *, size);

    free(begin_ptr as void *mut);

    return;
};

fun println_num(n: u64, base: u64): void {
    print_num(n, base);
    print_str("\n", 1);
    return;
};

fun get_args_arr(argc: u64, args: char *): char ** {

    let buff: char **mut = malloc(argc * 8) as char **mut;

    let mut curr_begin: char * = args;
    let mut curr: char * = curr_begin;

    let mut count: u64 = 0;

    while true {
        if count == argc { break; };
        
        if *curr == '\0' {
            buff[count] @= curr_begin;
            count += 1;

            curr += 1;
            curr_begin = curr;
            continue;
        };
        
        curr += 1;
    };

    return buff as char **;
};


fun memcpy(dest: void *mut, src: void *, n: u64): void {
    let a: char *mut = dest as char *mut;
    let b: char * = src as char *;
    for let mut i: u64 = 0; i < n; i += 1 {
        a[i] @= *b[i];
    };
    return;
};

fun memset(b: void *mut, c: u64, len: u64): void *mut {
    let mut i: u64 = 0;
    let mut p: char *mut = b as char *mut;

    while len > 0 {
        p @= c as char;
        p += 1;
        len -= 1;
    };

    return b;
};